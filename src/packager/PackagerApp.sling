
/*
 * This file is part of Eqela Rocks
 * Copyright (c) 2018 Eqela Oy
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; version 3 of the License.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program. If not, see <https://www.gnu.org/licenses/>.
 */

class is ConsoleApplicationMain:

import capex.console
import capex.archive

var source as File
var outputDir as File
var type as string
var wixPath as File
var packageInfoDetails = new DynamicMap()

func initialize override as bool
{
	foreach arg in args {
		if arg.isOption("type") {
			type = arg.value
		}
		else if arg.isOption("wix") {
			wixPath = File.forPath(arg.value)
		}
		else if arg.isOption("output") {
			outputDir = File.forPath(arg.value)
		}
		else if arg.isParameter() {
			assert source == null:
				Log.error(ctx, "Only one source path can be specified")
			source = File.forPath(arg.parameter)
		}
		else if arg.isOption() && String.startsWith(arg.getKey(), "D") {
			packageInfoDetails.set(String.getSubString(arg.getKey(), 1), arg.value)
		}
		else {
			arg.reportAsUnsupported(ctx)
			return false
		}
	}
	if String.isEmpty(type) {
		if OS.isWindows() {
			type = "msi"
		}
		else if OS.isLinux() {
			type = "deb"
		}
		else if OS.isMacOS() {
			type = "pkg"
		}
	}
	return true
}

func execute override as bool
{
	assert source:
		Log.error(ctx, "No source path provided.")
	var sourceDir as File
	var tmpDir as File
	if source.isFile() {
		if source.hasExtension("zip") {
			assert tmpDir = TemporaryDirectory.create():
				Log.error(ctx, "Failed to create a temporary directory")
			Log.status(ctx, "Extracting files: `" .. source.getPath() .. "' .. ")
			assert ZipReader.extractZipFileToDirectory(source, tmpDir) {
				Log.error(ctx, "Failed to extract zip file: '" .. source.getPath() .. "'.")
				tmpDir.removeRecursive()
			}
			Log.status(ctx, null)
			sourceDir = tmpDir
		}
		else if TarReader.isTarFile(source) {
			assert tmpDir = TemporaryDirectory.create():
				Log.error(ctx, "Failed to create a temporary directory")
			Log.status(ctx, "Extracting files: `" .. source.getPath() .. "' .. ")
			assert TarReader.extractTarFileToDirectory(source, tmpDir) {
				Log.error(ctx, "Failed to extract tar file: '" .. source.getPath() .. "'.")
				tmpDir.removeRecursive()
			}
			Log.status(ctx, null)
			sourceDir = tmpDir
		}
		else {
			Log.error(ctx, "Unsupported source file type: `" .. source.getPath() .. "'")
			return false
		}
	}
	else if source.isDirectory() {
		sourceDir = source
	}
	else {
		Log.error(ctx, "Not a file or directory: `" .. source.getPath() .. "'")
		return false
	}
	loop {
		var entry as File
		var it = sourceDir.entries()
		while it {
			var ee = it.next()
			if not ee:
				break
			if entry == null {
				entry = ee
			}
			else {
				entry = null
				break
			}
		}
		if not entry:
			break
		sourceDir = entry
	}
	var v = doExecute(sourceDir)
	if tmpDir:
		tmpDir.removeRecursive()
	return v
}

func doExecute(sourceDir as File) as bool
{
	var pi = sourceDir.entry("packageinfo.json")
	if pi.isFile() {
		var pic = assert JSONParser.parse(pi) as DynamicMap:
			Log.error(ctx, "Invalid format for package info file: `" .. pi.getPath() .. "'")
		foreach key in pic.getKeys() {
			if not packageInfoDetails.getString(key):
				packageInfoDetails.set(key, pic.getString(key))
		}
	}
	var packageInfo = new PackageInfo()
	packageInfo.fromJsonObject(packageInfoDetails)
	assert String.isNotEmpty(packageInfo.getVendor()):
		Log.error(ctx, "No vendor specified")
	assert String.isNotEmpty(packageInfo.getName()):
		Log.error(ctx, "No name specified")
	assert String.isNotEmpty(packageInfo.getVersion()):
		Log.error(ctx, "No version specified")
	assert String.isNotEmpty(packageInfo.getPlatform()):
		Log.error(ctx, "No target platform/architecture specified")
	assert String.isNotEmpty(packageInfo.getAuthorName()):
		Log.error(ctx, "No author name specified")
	assert String.isNotEmpty(packageInfo.getAuthorEmail()):
		Log.error(ctx, "No author email specified")
	assert String.isNotEmpty(packageInfo.getDescription()):
		Log.error(ctx, "No description specified")
	if String.isEmpty(packageInfo.getTitle()):
		packageInfo.setTitle(packageInfo.getName())
	var od = outputDir
	if od == null:
		od = source.getParent()
	var maker as PackageMaker
	if type == "deb" {
		maker = new DebMaker()
	}
	else if type == "msi" {
		var msi = new MsiMaker()
		msi.setWixPath(wixPath)
		maker = msi
	}
	else if type == "pkg" {
		maker = new PkgMaker()
	}
	assert maker:
		Log.error(ctx, "Unsupported package type: `" .. type .. "'")
	assert maker.execute(ctx, sourceDir, od, packageInfo):
		Log.error(ctx, "Package generation failed.")
	return true
}

main:
	return executeMain(new this(), args)
